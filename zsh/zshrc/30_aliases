# zshrc/30_aliases
#
# Copyright Abaakouk Mehdi <sileht@sileht.net>
# Released under the terms of the Artistic Licence 2.0
#
# Defines command shortcuts
#

autoload -U allopt
alias allopt="allopt |less"

alias stsrv="telnet www.listen-project.org 79 2>/dev/null | grep --color=none oco"

mka () {
	schedtool -B -n 1 -e ionice -n 1 make -j $(cat /proc/cpuinfo | grep "^processor" | wc -l) "$@"
}

imka () { 
    schedtool -D -n 19 -e ionice -c 3 make -j $(cat /proc/cpuinfo | grep "^processor" | wc -l) "$@" 
}

alias idletask='schedtool -D -n 19 -e ionice -c 3'
alias batchtask='schedtool -B -n 1 -e ionice -n 1'

andropath="/home/sileht/workspace/android/mydroid"
function getcm(){
    name=$(basename $(ssh sileht@home.sileht.net -p443 'ls -1 '$andropath'/update-sm-*-signed.zip' | sort -n | tail -1))
    echo "* Last version: $name"
    adb shell 'rm -f /sdcard/update-sm-*-signed.zip'
    [ "$(pwd)" != "$andropath" ] && rm -f update-sm-*-signed.zip
    scp -P443 sileht@home.sileht.net:$andropath/$name . && adb push ${name} /sdcard/ && \
    [ "$(pwd)" != "$andropath" ] && rm -f $name
}

function gd(){
    already_in_source=$(pwd | grep 'mydroid')
    cd $andropath
    source build/envsetup.sh
    [ -n "$already_in_source" ] && cd - >/dev/null
}

alias rm="nocorrect rm -i"
alias mv="nocorrect mv -i"
alias cp="nocorrect cp -i"
alias ln='nocorrect ln'
alias mkdir='nocorrect mkdir'
if [ $(id -u) -eq 0 ]; then
	alias sudo=""
else
	alias sudo="nocorrect sudo"
fi
alias wget='noglob wget'
alias curl='noglob curl'
alias apt-cache='noglob apt-cache'
alias man="LANG=C man"
alias ssh="ssh -C"
alias df="df -h"
alias diff='diff -rNu'
alias t='tail -F'
alias cal='cal -3'
alias vim="TERM=xterm vim"
alias vi="vim"
alias s="sudo"
alias svi="s vim" 
alias getaptkey='sudo apt-key adv --recv-keys --keyserver keyserver.ubuntu.com'

alias u="sudo -i -u"

#APT DPKG stuff
#function dpkg(){ sudo /usr/bin/dpkg $* ; rehash}
function aptcleanup(){
    sudo dpkg --clear-avail
    sudo dpkg --forget-old-unavail
    sudo sync-available
}

alias apt="sudo aptitude"

function of() { lsof -np "$1" }
compdef _pids of
function psgrep() { ps aux | grep "[${1[1]}]${1[2,-1]}" }

# QUICK EDIT
alias zal="vim $HOME/.zsh/zshrc/30_aliases"
alias zvi="vim $HOME/.vimrc"
alias zsc="vim $HOME/.screenrc"

# FIND STUFF
alias locate='noglob locate'
alias find='noglob find'
alias qf='find . -iname '
alias findnosecure="find . -perm +4000 -print"
function sfind(){ find "$@" | egrep -v '(binaire|\.svn)' ; }

# GREP STUFF
alias grep='grep --color=auto'
alias egrep='egrep --color=auto'
alias fgrep='fgrep --color=auto'
function sgrep(){ grep "$@" --color=always| egrep -v '(binaire|\.svn)' ; } 
function g(){ grep --color=always "$@" | more }

# ZSH STUFF
alias zmv="nocorrect noglob zmv"
alias mmv="nocorrect noglob zmv -W"
alias zcp='zmv -C'
alias zln='zmv -L'

# LS stuff
_LS_OPTS='-bCF --color=auto'
ls --group-directories-first >/dev/null 2>&1 \
  && _LS_OPTS="$_LS_OPTS --group-directories-first"
# I prefer POSIX collation for ls
alias ls="LC_COLLATE=POSIX ls -h --color=auto $_LS_OPTS"
alias ll="ls -lF"
alias la="ls -aF"
alias lla="ls -alF"
alias lsd='ls -ld *(-/DN)'
alias lsdir="for dir in *;do;if [ -d \$dir ];then;du -hsL \$dir 2>/dev/null;fi;done"
alias lsbig="ls -flh *(.OL[1,10])"
alias lssmall="ls -Srl *(.oL[1,10])"
alias lsnew="ls -rl *(D.om[1,10])"
alias lsold="ls -rtlh *(D.om[1,10])"
function l(){ ls -hla --color="always" "$@" | more }
alias vless='vim -u /usr/share/vim/vim71/macros/less.vim'

unset _LS_OPTS

# DISPLAY STUFF
function gkill(){ #MOTD
    filter="$@"
    ps xaf | grep "$filter" | grep -v grep 
    echo "Sure kill this process?"
    read 
    ps xaf | grep "$filter" | grep -v grep | awk '{print $1}' | xargs kill
}

# handy documentation lookup on Debian
# from http://www.michael-prokop.at/computer/config/.zshrc
doc() { cd /usr/share/doc/$1 && ls } #MOTD
_doc() { _files -W /usr/share/doc -/ }
compdef _doc doc

alias cmd="wineconsole --backend=curses cmd"

# LOG STUFF
function usniff(){sudo lsof -u$1|egrep -v "(/usr|/dev|/bin|/lib|socket$|pipe)"} # MOTD
function sudolog(){ sed -n -e "/sudo:.*COMMAND/$@p" /var/log/auth.log } #MOTD
function sulog(){ sed -n -e "/su:.*COMMAND/$@p" /var/log/auth.log } #MOTD
function showlog(){ tail -f /var/log/{syslog,messages,daemon.log} }

function killd(){ #MOTD
    DISPLAY="" ps xae | grep DISPLAY=:$1 | grep -v grep | awk '{print $1}' | xargs kill -9
}

function rappel(){
    timespec="$@"
    if [ "${timespec[1]}" == "+" ]; then
        timespec="now $timespec"
    fi
    echo -n "Sujet: " 
    read title
    at "$timespec" <<EOF
mail -s '$title' sileht@sileht.net <<maileof
$(cat)
maileof
EOF
}

# Service stuff
alias invoke-rc.d="nocorrect sudo invoke-rc.d"
alias service="nocorrect sudo service"
compdef _service invoke-rc.d

# CD STUFF
function cdw(){ cd $(which "$@") } #MOTD
function cd () {
if [[ -z $2 ]]; then
  if [[ -f $1 ]]; then
    builtin cd $1:h
  else
    if [[ $1 = ".." ]]; then
        new_pwd=${PWD%/*}
        [ -z "$new_pwd" ] && new_pwd="/"
        builtin cd $new_pwd
    else
        builtin cd $1
    fi
  fi
else
  if [[ -z $3 ]]; then
    builtin cd $1 $2
  else
    echo cd: too many arguments
  fi
fi
}

# vim:ft=zsh
